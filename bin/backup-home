#! /bin/zsh
# contents: dar(1) backup wrapper.
#
# Copyright © 2004,2007,2008 Nikolai Weibull <now@bitwi.se>

root=$HOME
dar_options="--fs-root $root
             --no-warn
             --mincompr 1024
             --bzip2
             --slice 700M
             --empty-dir
             -Q

             --exclude-compression *.gz
             --exclude-compression *.bz2
             --exclude-compression *.gif
             --exclude-compression *.jpeg
             --exclude-compression *.jpg
             --exclude-compression *.png
             --exclude-compression *.tgz
             --exclude-compression */.git/objects/*
             --exclude-compression */.git/index

             --prune .Trash
             --prune .adobe
             --prune .bash_history
             --prune .fonts.cache-1
             --prune .gconfd
             --prune .gnome
             --prune .gnome2
             --prune .local/etc
             --prune .local/opt
             --prune .local/lib/wine
             --prune .local/var/cache
             --prune .local/var/log
             --prune .local/var/run
             --prune .local/var/tmp
             --prune .revdep-rebuild*
             --prune .mozilla/Default*
             --prune .mozilla/firefox/*/Cache
             --prune .recently-used.xbel
             --prune .subversion
             --prune .thumbnails
             --prune inc
             --prune media
             --prune src/remote
             --prune tmp
             --prune */.deps
             --prune */.libs
             --prune */config
             --prune */autom4te.cache

             --exclude lock
             --exclude aclocal.m4
             --exclude config.log
             --exclude config.status
             --exclude configure
             --exclude core
             --exclude core.*
             --exclude *~
             --exclude *.class
             --exclude *.mp3
             --exclude *.o
             --exclude *.swp

             --batch $HOME/.local/var/lib/dar/password"
backupdest=~/tmp/backup
datestring=$(date +%Y-%m-%d)
filename=$backupdest/${${root#/}//\//-}-$datestring
filenames=($backupdest/*.dar(Nom))

if [[ $1 == diff ]]; then
  if (( $#filenames == 0 )); then
    print -u 2 "No previous backup"
    exit 1
  fi
  filename=$filename-diff
  dar_options+=" --ref ${${filenames[1]:r}%%.[[:digit:]]##}"
fi

dar --create $filename ${=dar_options} |& \
  sed '/Warning, the archive .* has been encrypted. A wrong key is not possible to detect, it would cause DAR to report the archive as corrupted/d
        /^-J is only useful with -A option, for the archive of reference$/d
        /^$/d
        /^ --------------------------------------------$/d
        /^ [0-9]\+ inode(s) saved$/d
        /^ with [0-9]\+ hard link(s) recorded$/d
        /^ [0-9]\+ inode(s) changed at the moment of the backup$/d
        /^ [0-9]\+ inode(s) not saved (no inode\/file change)/d
        /^ 0 inode(s) failed to save (filesystem error)$/d
        /^ [0-9]\+ inode(s) ignored (excluded by filters)$/d
        /^ [0-9]\+ inode(s) recorded as deleted from reference backup$/d
        /^ Total number of inode considered: [0-9]\+$/d
        /^ EA saved for [0-9]\+ inode(s)$/d'

eval $(keychain --eval --quiet --noask --dir $HOME/.local/var/cache/keychain id_dsa)
ssh-add -l 2>/dev/null | grep -q id_dsa || { print -u 2 'Can’t find id_dsa key'; exit 1 }
scp -q $filename.[[:digit:]]##.dar shell.scorpionshops.com:
