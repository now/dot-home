#! /bin/zsh
# contents: df(1) wrapper with better formatted output.
#
# Copyright Â© 2008 Nikolai Weibull <now@bitwi.se>

setopt extendedglob

declare -a data
data=(Filesystem Type Size Used Free Used "Mounted on"
      ${=${(f)"$(${(f)$(whence -p {g,}df)[1]} --exclude-type=devfs --exclude-type=tmpfs --si --print-type --portability | tail -n +2 | sort -k 7)"}})

declare -a lengths
lengths=(0 0 0 0 0 0 0)

integer i
for (( i = 1; i <= 7; i++ )); do
  integer j
  for (( j = i; j <= $#data; j += 7 )); do
    integer length=$#data[j]
    (( lengths[i] = length > lengths[i] ? length : lengths[i] ))
  done
done

print-row () {
  local format=$1; shift
  declare -a args
  integer i
  for (( i = 1; i <= 6; i++ )); do
    args+=$lengths[i]
    args+=$1
    shift
  done
  args+=$*

  printf $format $args
}

print-row "%-*s  %-*s  %-*s  %-*s  %-*s  %-*s  %s\n" $data[1,7]
for (( i = 8; i <= $#data; i += 7 )); do
  print-row "%-*s  %-*s  %*s  %*s  %*s  %*s  %s\n" $data[i,i+7]
done

