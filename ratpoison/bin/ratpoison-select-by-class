#! /bin/zsh
# contents: Ratpoison command to select a window by resource class.
#
# Copyright © 2006 Nikolai Weibull <now@bitwi.se>

: ${RATPOISON:?ratpoison isn’t running as your windowmanager}

rp-error () {
  rp-call echo $@
  exit
}

if (( $# < 1 )); then
  rp-error 'Missing argument'
fi

rp-call () {
  $RATPOISON -c "$*"
}

rp-window-list () {
  set -A $1 ${(f)"$(rp-call windows %s%c)"}
}

rp-active-window-class () {
  local -a windows
  rp-window-list windows
  print -- ${windows[(r)\**]#\*}
}

rp-check-result () {
  if [[ $1 != "" ]]; then
    rp-error $1
  fi
}

rp-select-by-class () {
  winname_save=$(rp-call 'set winname')
  rp-call 'set winname class'
  result=$(rp-call "select $class" 2>&1)
  rp-call "set winname $winname_save"
  rp-check-result $result
}

class=$1
if [[ $(rp-active-window-class) == $class ]]; then
  rp-check-result "$(rp-call 'inext' 2>&1)"
else
  rp-select-by-class $class
fi
