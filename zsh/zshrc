# Shell and Terminal Settings

HISTFILE=${XDG_CACHE_HOME:-~/.cache}/zsh/history
SAVEHIST=${HISTSIZE::=1024}
REPORTTIME=300

setopt completeinword nolistambiguous nolisttypes \
       extendedhistory histnostore \
       rcexpandparam \
       combiningchars ignoreeof \
       nonotify \
       promptsubst \
       nobeep

if [[ $TERM != dumb ]]; then
  PS1='%(?..; \$?=%9F%?%f)${SSH_CONNECTION:+; ssh %M}; cd %~; '
  PS2='${(l|$((2 * ${(w)#${(%%):-%_}}))|)}'
  PS3='Your selection: '
  PS4='+%N:%i: '
fi

TIMEFMT=%J$'\n''real: %*E, user: %*U, system: %*S (%P)'

autoload -U add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
add-zsh-hook zsh_directory_name zsh_directory_name_cdr
add-zsh-hook zsh_directory_name dynamic-directory-names

zstyle ':dircolors:*' cache-policy dircolors-cache-policy
{
  dircolors-cache-policy () {
    [[ ${XDG_CONFIG_HOME:-~/.config}/dircolors -nt $1 ]]
  }
  autoload -U cache-retrieve
  cache-retrieve ':dircolors:*' dircolors-$TERM
} always {
  unfunction dircolors-cache-policy >&/dev/null
}
if [[ -z $LS_COLORS ]]; then
  eval $(${commands[gdircolors]:-dircolors} ${XDG_CONFIG_HOME:-~/.config}/dircolors 2>/dev/null)
  autoload -U cache-store
  cache-store ':dircolors:*' dircolors-$TERM LS_COLORS
else
  export LS_COLORS
fi

# Modules

zmodload -i zsh/complist

# Command-line Completion

autoload -U compinit; compinit

zstyle ':completion:*' accept-exact-dirs true
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' 'r:|[:.,_-]=** r:|=**'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-separator ''
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:descriptions' format '%K{white}%d%k'
zstyle ':completion:*:messages' format '%F{green}%d%f'
zstyle ':completion:*:warnings' format '%F{red}no matches for %d%f'
zstyle ':completion:*:options' auto-description 'specify %d'
zstyle ':completion::complete:*' use-cache yes

zstyle ':completion:*:processes' command 'ps -U $EUID 2>/dev/null'
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:*:cdr:*:*' menu selection
zstyle ':completion:*:*:kill:*:*' menu selection

# Keybindings

autoload -U forward-word-match backward-word-match \
            backward-kill-word-match

for command in $ZDOTDIR/functions/zle/*(e:'REPLY=${REPLY#$ZDOTDIR/functions/}':); do
  autoload -U $command
  zle -N $command
done

unset command

zle -N backward-kill-to-space-or-/ backward-kill-word-match
zstyle ':zle:backward-kill-to-space-or-/' word-chars '*?_-.[]~=&;!#$%^(){}<>:@,\\'

zle -N forward-parameter forward-word-match
zle -N backward-parameter backward-word-match
zstyle ':zle:(forward|backward)-parameter' word-style shell

autoload -U insert-composed-char
zle -N insert-composed-char

bindkey -arp '^['
bindkey -a \
  '(' backward-parameter \
  ')' forward-parameter \
  '/' history-incremental-search-backward \
  '?' history-incremental-search-forward \
  '^D' zle/foreground-or-list-choices \
  '^O' accept-line-and-down-history \
  '^R' redo \
  '^U' zle/up-directory \
  '^^' zle/cd-to-alternate-directory \
  'L' vi-change-whole-line \
  'l' vi-substitute \
  's' vi-forward-char \
  'u' undo

bindkey -rp '^['
bindkey \
  '^?' backward-delete-char \
  '^D' zle/vi-cmd-mode-silently \
  '^H' backward-delete-char \
  '^K' insert-composed-char \
  '^O' accept-line-and-down-history \
  '^P' push-line-or-edit \
  '^U' zle/up-from-menu \
  '^W' backward-kill-to-space-or-/ \
  '^X^L' zle/history-beginning-search-menu \
  '^X^S' zle/sudo-command-line \
  '^Xu' zle/urlify-current-argument \
  '^Y' zle/yank-clipboard \
  '^^' zle/cd-to-alternate-directory \
  ' ' magic-space \
  ${(s: :):-${^${(s::):-'|<>&'}}' zle/self-insert-redir'}
bindkey -r '^['

# Functions And Aliases

autoload -U cdr zargs zcalc zmv freload hc up urlify

alias \
  h='fc -lfiD' \
  mmv='noglob zmv -W'

# Operating System And Host Specific Settings

source $ZDOTDIR/os/rc
source $ZDOTDIR/host/rc
