# Shell and Terminal Settings

HISTFILE=~/.zshistory
SAVEHIST=${HISTSIZE::=1024}
REPORTTIME=300
unset TERMCAP

setopt autocd \
       completeinword nolistambiguous nolisttypes \
       extendedhistory histnostore \
       rcexpandparam \
       combiningchars dvorak ignoreeof \
       nonotify \
       nobeep

autoload -U add-zsh-hook
add-zsh-hook precmd set-terminal-title-to-pwd
add-zsh-hook preexec set-terminal-title-from-command
add-zsh-hook chpwd list-directory-on-chpwd
add-zsh-hook chpwd chpwd_recent_dirs
add-zsh-hook zsh_directory_name zsh_directory_name_cdr
add-zsh-hook zsh_directory_name dynamic-directory-names

zstyle ':dircolors:*' cache-policy dircolors-cache-policy
{
  dircolors-cache-policy () {
    [[ ~/.dircolors -nt $1 ]]
  }
  autoload -U cache/retrieve
  cache/retrieve ':dircolors:*' dircolors-$TERM
} always {
  unfunction dircolors-cache-policy >&/dev/null
}
if [[ -z $LS_COLORS ]]; then
  eval $(${commands[gdircolors]:-dircolors} ~/.dircolors 2>/dev/null)
  autoload -U cache/store
  cache/store ':dircolors:*' dircolors-$TERM LS_COLORS
else
  export LS_COLORS
fi

# Modules

zmodload -i zsh/complist

# Command-line Completion

autoload -U compinit; compinit

zstyle ':completion:*' accept-exact-dirs true
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' 'r:|[:.,_-]=** r:|=**'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-separator ''
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion::complete:*' use-cache yes

zstyle ':completion:*:processes' command 'ps -U $EUID 2>/dev/null'
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:*:cdr:*:*' menu selection
zstyle ':completion:*:*:kill:*:*' menu selection

# Keybindings

autoload -U forward-word-match backward-word-match \
            replace-string backward-kill-word-match

for command in $ZDOTDIR/functions/zle/* \
           copy-earlier-word; do
  command=${command#$ZDOTDIR/functions/}
  autoload -U $command
  zle -N $command
done

zle -N backward-kill-to-space-or-/ backward-kill-word-match
zstyle ':zle:backward-kill-to-space-or-/' word-chars '*?_-.[]~=&;!#$%^(){}<>:@,\\'

zle -N forward-parameter forward-word-match
zle -N backward-parameter backward-word-match
zstyle ':zle:(forward|backward)-parameter' word-style shell

bindkey -arp '^['
bindkey -a \
  '(' backward-parameter \
  ')' forward-parameter \
  '/' history-incremental-search-backward \
  '?' history-incremental-search-forward \
  '^D' zle/foreground-or-list-choices \
  '^O' accept-line-and-down-history \
  '^R' redo \
  '^U' zle/up-directory \
  '^^' zle/cd-to-alternate-directory \
  'L' vi-change-whole-line \
  'l' vi-substitute \
  's' vi-forward-char \
  'u' undo

bindkey -rp '^['
bindkey \
  '^?' backward-delete-char \
  '^D' zle/vi-cmd-mode-silently \
  '^H' backward-delete-char \
  '^K' zle/insert-digraph \
  '^N' _history-complete-newer \
  '^O' accept-line-and-down-history \
  '^P' _history-complete-older \
  '^Q' push-line-or-edit \
  '^U' zle/up-from-menu \
  '^\' undo \
  '^W' backward-kill-to-space-or-/ \
  '^X$' insert-last-word \
  '^X^L' zle/history-beginning-search-menu \
  '^X^P' copy-earlier-word \
  '^X^S' zle/sudo-command-line \
  '^Xu' zle/urlify-current-argument \
  '^Y' zle/yank-clipboard \
  '^^' zle/cd-to-alternate-directory \
  ' ' magic-space \
  ${(s: :):-${^${(s::):-'|<>&'}}' zle/self-insert-redir'}
bindkey -r '^['

# Functions And Aliases

autoload -U promptinit; promptinit
prompt now

autoload -U zsh-mime-setup; zsh-mime-setup

autoload -U cdr zargs zcalc zmv $ZDOTDIR/functions/autoload/*(:t)

zstyle ':chpwd:*' recent-dirs-default true

alias mmv='noglob zmv -W'

unset command

alias \
  g='grep --color -n -P' \
  h='fc -lfiD' \
  p='ps -U $EUID -o "pid= comm=" -H' \
  pp='ps -U $EUID -o "pid= command=" -H' \
  top='top -U $EUID'

alias \
  ls="${commands[gls]:-ls} --color" \
  a='ls' \
  o='ls -l --si' \
  aa='a -A' \
  oo='o -A'

# Operating System And Host Specific Settings

source $ZDOTDIR/os/rc
source $ZDOTDIR/host/rc
