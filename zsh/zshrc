# contents: zsh(1) user RC.
#
# Copyright © 2002,2003,2004,2005,2006,2008 Nikolai Weibull <now@bitwi.se>

# 1  Shell and Terminal Settings
# ——————————————————————————————

HISTFILE=~/.zshistory
SAVEHIST=${HISTSIZE::=1024}
REPORTTIME=300
unset TERMCAP

setopt autocd autopushd                           \
       completeinword nolistambiguous nolisttypes \
       extendedhistory histnostore                \
       rcexpandparam rcquotes                     \
       correct dvorak noflowcontrol               \
       nonotify                                   \
       nobeep

autoload -U add-zsh-hook
add-zsh-hook precmd set-terminal-title-to-pwd
add-zsh-hook preexec set-terminal-title-from-command
add-zsh-hook chpwd list-directory-on-chpwd
add-zsh-hook chpwd update-pwd-history-on-chpwd

# TODO: Move this to its own function?
zstyle ':dircolors:*' cache-policy dircolors-cache-policy
{
  dircolors-cache-policy () {
    [[ ~/.dircolors -nt $1 ]]
  }
  autoload -U cache/retrieve
  cache/retrieve ':dircolors:*' dircolors-$TERM
} always {
  unfunction dircolors-cache-policy >& /dev/null
}
if [[ -z $LS_COLORS ]]; then
  eval $(${commands[gdircolors]:-dircolors} ~/.dircolors 2> /dev/null)
  autoload -U cache/store
  cache/store ':dircolors:*' dircolors-$TERM LS_COLORS
else
  export LS_COLORS
fi

# 2  Modules
# ——————————

zmodload -i zsh/complist


# 3  Command-line Completion
# ——————————————————————————

autoload -U compinit; compinit

zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' 'r:|[:.,_-]=** r:|=**'
zstyle ':completion:*' group-name ""
zstyle ':completion:*' list-separator ""
zstyle ':completion:*:options' auto-description "specify ‘%d’"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion::complete:*' use-cache yes

zstyle ':completion:*:processes' command 'ps -U $EUID 2>/dev/null'
zstyle ':completion:*:manuals' separate-sections true


# 4  ZLE Keybindings
# ——————————————————

autoload -U forward-word-match backward-word-match \
            replace-string backward-kill-word-match

for command in zle/vim-increase-number zle/self-insert-redir zle/up-directory \
               zle/cd-from-list zle/cd-to-alternate-directory \
               zle/urlify-current-argument \
               zle/yank-clipboard zle/sudo-command-line \
               history-beginning-search-menu insert-digraph copy-earlier-word; do
  autoload -U $command
  zle -N $command
done

source $fpath/keeper(N)

zle -N zle/vim-decrease-number zle/vim-increase-number
zle -N replace-pattern replace-string

zle -N backward-kill-to-space-or-/ backward-kill-word-match
zstyle ':zle:backward-kill-to-space-or-/' word-chars '*?_-.[]~=&;!#$%^(){}<>:@,\\'

zle -N forward-parameter forward-word-match
zle -N backward-parameter backward-word-match
zstyle ':zle:(forward|backward)-parameter' word-style shell

zle -C complete-kept-result complete-word _generic
zstyle ':completion:complete-kept-result:*' completer _insert_kept

bindkey -arp '^['
bindkey -a \
  '(' backward-parameter \
  ')' forward-parameter \
  '/' history-incremental-search-backward \
  '?' history-incremental-search-forward \
  '^A' zle/vim-increase-number \
  '^G' zle/cd-from-list \
  '^O' accept-line-and-down-history \
  '^R' redo \
  '^U' zle/up-directory \
  '^X' zle/vim-decrease-number \
  '^^' zle/cd-to-alternate-directory \
  'L' vi-change-whole-line \
  'l' vi-substitute \
  's' vi-forward-char \
  'u' undo \
  ':s' replace-pattern

bindkey -rp '^['
bindkey \
  '^?' backward-delete-char \
  '^D' list-choices \
  '^H' backward-delete-char \
  '^K' insert-digraph \
  '^N' _history-complete-newer \
  '^O' accept-line-and-down-history \
  '^P' _history-complete-older \
  '^Q' push-line-or-edit \
  '^U' undo \
  '^W' backward-kill-to-space-or-/ \
  '^X$' insert-last-word \
  '^X^K' complete-kept-result \
  '^Xk' complete-kept-result \
  '^X^L' history-beginning-search-menu \
  '^X^P' copy-earlier-word \
  '^X^S' zle/sudo-command-line \
  '^Xu' zle/urlify-current-argument \
  '^Y' zle/yank-clipboard \
  '^Z' vi-cmd-mode \
  '^^' zle/cd-to-alternate-directory \
  ' ' magic-space \
  ${(s: :):-${^${(s::):-'|<>&'}}' zle/self-insert-redir'}


# 5  Functions And Aliases
# ————————————————————————

autoload -U promptinit; promptinit
prompt now

autoload -U zsh-mime-setup; zsh-mime-setup

autoload -U zargs zcalc zmv $ZDOTDIR/functions/autoload/*(:t)

for command in cp mv mkdir; do
  alias $command="nocorrect $command"
done

for command in dr foldl foldr j ri; do
  alias $command="noglob $command"
done
alias mmv='noglob zmv -W'

unset command

alias \
  g='grep --color -n -P' \
  h='fc -lfiD' \
  p='ps -U $EUID -o "pid= comm=" -H' \
  pp='ps -U $EUID -o "pid= command=" -H' \
  top='top -U $EUID'

duh () { (($# > 0)) || set *; ${commands[gdu]:-du} --si -s ${2:+-c} $* }

alias \
  m='mplayer' \
  tv='DISPLAY=:0.1 mplayer -geometry 1024x600+0+75' \
  ro='vimless' \
  v='vim' \

alias \
  ls="${commands[gls]:-ls} --color" \
  a='ls' \
  o='ls -l --si' \
  aa='a -A' \
  oo='o -A'


# 11  Operating System And Host Specific Settings
# ———————————————————————————————————————————————

source $ZDOTDIR/{os,host}/rc
