[[ $TERM = dumb ]] && unsetopt zle && PS1='$ ' && return

HISTFILE=@zshistory@
SAVEHIST=${HISTSIZE::=8192}
REPORTTIME=300

setopt completeinword nolistambiguous nolisttypes \
       extendedhistory histignorespace histnostore \
       rcexpandparam \
       combiningchars \
       nonotify \
       promptsubst \
       nobeep

PS1='%(?..; (exit %9F%?%f%))${SSH_CONNECTION:+; ssh %M}; cd %~; '
PS2='${(l|$((2 * ${(w)#${(%%):-%_}}))|)}'
PS3='Your selection: '
PS4='+%N:%i: '

TIMEFMT=%J$'\n''real: %*E, user: %*U, system: %*S (%P)'

zmodload -i zsh/complist

autoload -U compinit; compinit -d '@zshcompletiondump@'

zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' 'r:|[:.,_-]=** r:|=**'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-separator ''
zstyle ':completion:*:descriptions' format '%K{white}%d%k'
zstyle ':completion:*:messages' format '%F{green}%d%f'
zstyle ':completion:*:warnings' format '%F{red}no matches for %d%f'
zstyle ':completion:*:options' auto-description 'specify %d'
zstyle ':completion::complete:*' cache-path '@zshcompletioncachedir@'
zstyle ':completion::complete:*' use-cache yes

zstyle ':completion:*:processes' command 'ps -U $EUID 2>/dev/null'
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:*:kill:*:*' menu selection

function auto-zle () { autoload -U $1; zle -N $1; }
auto-zle history-beginning-search-menu
unfunction auto-zle

bindkey \
  '^?' backward-delete-char \
  '^H' backward-delete-char \
  '^O' accept-line-and-down-history \
  '^X^L' history-beginning-search-menu \
  ' ' magic-space

autoload -U zargs hc up

alias h='fc -lfiD'

alias ,b="@EMACSCLIENT@ -e '(now-ivy-switch-buffer)' > /dev/null"
