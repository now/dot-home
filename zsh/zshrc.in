# Shell and Terminal Settings

HISTFILE=@zshistory@
SAVEHIST=${HISTSIZE::=8192}
REPORTTIME=300

setopt completeinword nolistambiguous nolisttypes \
       extendedhistory histnostore \
       rcexpandparam \
       combiningchars ignoreeof \
       nonotify \
       promptsubst \
       nobeep

if [[ $TERM != dumb ]]; then
  PS1='%(?..; \$?=%9F%?%f)${SSH_CONNECTION:+; ssh %M}; cd %~; '
  PS2='${(l|$((2 * ${(w)#${(%%):-%_}}))|)}'
  PS3='Your selection: '
  PS4='+%N:%i: '
fi

TIMEFMT=%J$'\n''real: %*E, user: %*U, system: %*S (%P)'

autoload -U add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
add-zsh-hook zsh_directory_name zsh_directory_name_cdr
add-zsh-hook zsh_directory_name dynamic-directory-names

# Modules

zmodload -i zsh/complist

# Command-line Completion

autoload -U compinit; compinit

zstyle ':completion:*' accept-exact-dirs true
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' 'r:|[:.,_-]=** r:|=**'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-separator ''
zstyle ':completion:*:descriptions' format '%K{white}%d%k'
zstyle ':completion:*:messages' format '%F{green}%d%f'
zstyle ':completion:*:warnings' format '%F{red}no matches for %d%f'
zstyle ':completion:*:options' auto-description 'specify %d'
zstyle ':completion::complete:*' use-cache yes

zstyle ':completion:*:processes' command 'ps -U $EUID 2>/dev/null'
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:*:cdr:*:*' menu selection
zstyle ':completion:*:*:kill:*:*' menu selection

# Keybindings

autoload -U forward-word-match backward-word-match \
            backward-kill-word-match

for command in cd-to-alternate-directory foreground-or-list-choices \
  history-beginning-search-menu insert-composed-char self-insert-redir \
  sudo-command-line up-directory urlify-current-argument vi-cmd-mode-silently \
  yank-clipboard; do
  autoload -U $command
  zle -N $command
done

unset command

zle -N backward-kill-to-space-or-/ backward-kill-word-match
zstyle ':zle:backward-kill-to-space-or-/' word-chars '*?_-.[]~=&;!#$%^(){}<>:@,\\'

zle -N forward-parameter forward-word-match
zle -N backward-parameter backward-word-match
zstyle ':zle:(forward|backward)-parameter' word-style shell

bindkey -arp '^['
bindkey -a \
  '(' backward-parameter \
  ')' forward-parameter \
  '/' history-incremental-search-backward \
  '?' history-incremental-search-forward \
  '^D' foreground-or-list-choices \
  '^O' accept-line-and-down-history \
  '^R' redo \
  '^U' up-directory \
  '^^' cd-to-alternate-directory \
  'L' vi-change-whole-line \
  'l' vi-substitute \
  's' vi-forward-char \
  'u' undo

bindkey -rp '^['
bindkey \
  '^?' backward-delete-char \
  '^D' vi-cmd-mode-silently \
  '^H' backward-delete-char \
  '^K' insert-composed-char \
  '^O' accept-line-and-down-history \
  '^P' push-line-or-edit \
  '^U' up-directory \
  '^W' backward-kill-to-space-or-/ \
  '^X^L' history-beginning-search-menu \
  '^Xu' urlify-current-argument \
  '^Y' yank-clipboard \
  '^^' cd-to-alternate-directory \
  ' ' magic-space \
  ${(s: :):-${^${(s::):-'|<>&'}}' self-insert-redir'}
bindkey -r '^['

# Functions And Aliases

autoload -U cdr zargs zcalc zmv freload hc up urlify

alias \
  h='fc -lfiD' \
  mmv='noglob zmv -W'

# Operating System And Host Specific Settings

source $ZDOTDIR/os/rc
