[[ $TERM = dumb ]] && unsetopt zle && PS1='$ ' && return

HISTFILE=@zshistory@
SAVEHIST=${HISTSIZE::=8192}
REPORTTIME=300

setopt completeinword nolistambiguous nolisttypes \
       extendedhistory histignorespace histnostore \
       rcexpandparam \
       combiningchars ignoreeof \
       nonotify \
       promptsubst \
       nobeep

PS1='%(?..; (exit %9F%?%f%))${SSH_CONNECTION:+; ssh %M}; cd %~; '
PS2='${(l|$((2 * ${(w)#${(%%):-%_}}))|)}'
PS3='Your selection: '
PS4='+%N:%i: '

TIMEFMT=%J$'\n''real: %*E, user: %*U, system: %*S (%P)'

autoload -U add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
add-zsh-hook zsh_directory_name zsh_directory_name_cdr
add-zsh-hook zsh_directory_name dynamic-directory-names

zmodload -i zsh/complist

autoload -U compinit; compinit

zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' 'r:|[:.,_-]=** r:|=**'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-separator ''
zstyle ':completion:*:descriptions' format '%K{white}%d%k'
zstyle ':completion:*:messages' format '%F{green}%d%f'
zstyle ':completion:*:warnings' format '%F{red}no matches for %d%f'
zstyle ':completion:*:options' auto-description 'specify %d'
zstyle ':completion::complete:*' use-cache yes

zstyle ':completion:*:processes' command 'ps -U $EUID 2>/dev/null'
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:*:kill:*:*' menu selection

autoload -U forward-word-match backward-word-match backward-kill-word-match

function auto-zle () { autoload -U $1; zle -N $1; }
auto-zle cd-to-alternate-directory
auto-zle foreground-or-list-choices
auto-zle history-beginning-search-menu
auto-zle insert-composed-char
auto-zle up-directory
auto-zle vi-cmd-mode-silently
unfunction auto-zle

zle -N backward-kill-to-space-or-/ backward-kill-word-match
zstyle ':zle:backward-kill-to-space-or-/' word-chars '*?_-.[]~=&;!#$%^(){}<>:@,\\'

zle -N forward-parameter forward-word-match
zle -N backward-parameter backward-word-match
zstyle ':zle:(forward|backward)-parameter' word-style shell

bindkey -arp '^['
bindkey -a \
  '(' backward-parameter \
  ')' forward-parameter \
  '/' history-incremental-search-backward \
  '?' history-incremental-search-forward \
  '^D' foreground-or-list-choices \
  '^O' accept-line-and-down-history \
  '^U' up-directory \
  '^^' cd-to-alternate-directory \
  'L' vi-change-whole-line \
  'U' redo \
  'l' vi-substitute \
  's' vi-forward-char \
  'u' undo

bindkey -rp '^['
bindkey -r '^['
bindkey \
  '^?' backward-delete-char \
  '^D' vi-cmd-mode-silently \
  '^H' backward-delete-char \
  '^K' insert-composed-char \
  '^O' accept-line-and-down-history \
  '^P' push-line-or-edit \
  '^U' up-directory \
  '^W' backward-kill-to-space-or-/ \
  '^X^L' history-beginning-search-menu \
  '^^' cd-to-alternate-directory \
  ' ' magic-space

autoload -U zargs hc up

alias h='fc -lfiD'

alias ,b="@EMACSCLIENT@ -e '(ido-switch-buffer)' > /dev/null"
