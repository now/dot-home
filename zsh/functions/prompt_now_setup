prompt_now_help () {
  emulate -L zsh

  cat <<EOH
Nothing to see here.
EOH
}

integer PSCOL=-1
PSZERO='%([BSUbfksu]|[0-9]#[FK]({[^}]##}|))'

prompt_now_precmd () {
  psvar[8]=$?
  (( psvar[8] )) || psvar[8]=

  vcs_info

  prompt_now_adjust_width || return

  local listprompt='More%R page: %p'
  integer fill
  (( fill = COLUMNS - ${#${${listprompt//\%R}//\%p/Top}} - 2 ))
  zstyle ':completion:*:default' list-prompt \
    "%K{white}${listprompt%\%R*}${(l:fill:)}${listprompt##*%R}%k"
}

prompt_now_winch () {
  prompt_now_adjust_width && zle && zle reset-prompt
}

prompt_now_adjust_width () {
  emulate -L zsh
  setopt extendedglob

  local saved_vcs_info_msg_0_=$vcs_info_msg_0_
  integer width

  psvar[9]=""
  vcs_info_msg_0_=${vcs_info_msg_0_//$~PSZERO}
  (( width = COLUMNS - ${#${(f)${(%%)${PS1//$~PSZERO/}}}[1]} ))
  vcs_info_msg_0_=$saved_vcs_info_msg_0_
  psvar[9]=${(l:width:)}

  (( COLUMNS == PSCOL )) && return 1
  (( PSCOL = COLUMNS ))
}

prompt_now_setup () {
  emulate -L zsh
  setopt extendedglob nolocaltraps
  prompt_opts=(cr percent subst)

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes yes
  zstyle ':vcs_info:*' stagedstr '%F{green}+'
  zstyle ':vcs_info:*' unstagedstr '%F{red}!'
  zstyle ':vcs_info:*' formats ' [%b%c%u%f]'
  zstyle ':vcs_info:*' actionformats ' [%a: %b%c%u%f]'
  autoload -U vcs_info

  prompt_now_teardown

  case $TERM in
    (xterm-256color|screen-256color) cs=('…' '“' '”') ;;
    (*)                              cs=('...' '"' '"') ;;
  esac

  local error='%8(V. [%9F%8v%f].)'
  local user=' %n@%m'
  local -ah ps1
  ps1=(
    '%249K'
    "%\$(( COLUMNS - \${#psvar[9]} ))<$cs[1]<"
    '%~'
    '%<<'
    $error
    \$vcs_info_msg_0_
    '%9v'
    $user
    '%k'
    $'\n'
  )
  PS1=${(j::)ps1}
  PS2='${(l|$((2 * ${(w)#${(%%):-%_}}))|)}'
  PS3='Your selection: '
  PS4='%F{yellow}+%F{blue}%N%f:%i: '

  SPROMPT="zsh: correct $cs[2]%9F%R%f$cs[3] to $cs[2]%10F%r%f$cs[3]? "

  TIMEFMT=%J$'\n''real: %*E user: %*U system: %*S (%P)'

  WATCHFMT='%(a.logon.logoff): %n (%M) tty: %l time: %T (%D)'

  (( $#mailpath > 0 )) && mailpath=(${^mailpath%\?*}'?new: $_')

  zstyle ':completion:*:descriptions' format '%K{white}%d%k'
  zstyle ':completion:*:messages' format '%F{green}%d%f'
  zstyle ':completion:*:warnings' format '%F{red}no matches for %d%f'
  zstyle ':completion:*:corrections' format '%K{white}%d (errors: %F{9}%e%f)%k'

  add-zsh-hook precmd prompt_now_precmd
  functions[TRAPWINCH]="${functions[TRAPWINCH]//$'\n'    prompt_now_winch}
    prompt_now_winch"
}

prompt_now_teardown () {
  emulate -L zsh

  add-zsh-hook -D precmd prompt_now_precmd
  functions[TRAPWINCH]="${functions[TRAPWINCH]//$'\n'    prompt_now_winch}"
}

prompt_now_preview () {
  local +h PS1='%# '
  prompt_preview_theme now "$@"
}

[[ -o kshautoload ]] || prompt_now_setup "$@"
