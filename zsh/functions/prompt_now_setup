prompt_now_help () {
  emulate -L zsh

  cat <<EOH
Nothing to see here.
EOH
}

prompt_now_chars () {
  case $TERM in
    (xterm-256color|screen-256color) cs=('…' '…' '“' '“' '”' '”') ;;
    (*)                              cs=('…' '...' '“' '"' '”' '"') ;;
  esac
}

prompt_now_setup () {
  emulate -L zsh
  setopt extendedglob
  prompt_opts=(cr percent subst)

  prompt_now_teardown

  local -A cs
  prompt_now_chars

  PS1='%(?..; \$?=%9F%?%f)${SSH_CONNECTION:+; ssh %M}; cd %~; '
  PS2='${(l|$((2 * ${(w)#${(%%):-%_}}))|)}'
  PS3='Your selection: '
  PS4='%F{yellow}+%F{blue}%N%f:%i: '

  SPROMPT="zsh: correct $cs[“]%9F%R%f$cs[”] to $cs[“]%10F%r%f$cs[”]? "

  TIMEFMT=%J$'\n''real: %*E, user: %*U, system: %*S (%P)'

  WATCHFMT='%(a.logon.logoff): %n (%M) tty: %l time: %T (%D)'

  (( $#mailpath > 0 )) && mailpath=(${^mailpath%\?*}'?new: $_')

  zstyle ':completion:*:default' list-prompt '%K{white}More (%p)%k'
  zstyle ':completion:*:descriptions' format '%K{white}%d%k'
  zstyle ':completion:*:messages' format '%F{green}%d%f'
  zstyle ':completion:*:warnings' format '%F{red}no matches for %d%f'
  zstyle ':completion:*:corrections' format '%K{white}%d (errors: %9F%e%f)%k'
  zstyle ':completion:*:options' auto-description "specify $cs[“]%d$cs[”]"
}

prompt_now_teardown () {
}

prompt_now_preview () {
  local +h PS1='%# '
  prompt_preview_theme now "$@"
}

[[ -o kshautoload ]] || prompt_now_setup "$@"
