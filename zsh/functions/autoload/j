emulate -L zsh
setopt extendedglob

if (( $# == 1 )) && [[ -d $1 ]]; then
  cd $1
  return $status
fi

local -A weights
source ~/.j 2> /dev/null

if (( $# == 0 )); then
  integer changed
  for key in ${(k)weights}; do
    if [[ ! -d $key ]] || (( $weights[$key] < 1 )); then
      unset "weights[$key]"
      (( changed = 1 ))
    fi
  done
  (( changed )) && print -r "weights=(${(kv@qq)weights})" > ~/.j
fi

local -A matches
local pattern="*${(j:*:)*}*" key
for key in ${${(kM)weights:#(#l)$~pattern}:-${(kM)weights:#(#i)$~pattern}}; do
  matches[$key]=$weights[$key]
done

local -a combined
for key in ${(k)matches}; do
  combined+=$(printf '%3.0f  %s' $matches[$key] $key)
done
combined=(${(O)combined})

if (( $# == 0 )); then
  print -r -l -- $combined | ${PAGER:-less}
  return
fi

if (( $#matches == 0 )); then
  print -r "$0: No directories match the given pattern" >&2
  return 1
fi

cd ${combined[1]##[[:space:]]#[[:digit:]]##[[:space:]]##}
