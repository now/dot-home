# contents: Set terminal’s title based on just started command.
#
# Copyright © 2008 Nikolai Weibull <now@bitwi.se>

emulate -L zsh

autoload -U terminal-title

local -a cmd

cmd=(${(z)1})
case $cmd[1] in
  (fg|%*)
    local spec=
    if [[ $cmd[1] == fg ]]; then
      (( $#cmd == 1 )) && spec=%+ || spec=${(Q)cmd[2]}
    else
      spec=${(Q)cmd[1]}
    fi
    cmd=(builtin jobs -l $spec)

    local -A jt
    jt=(${(kv)jobtexts})

    $cmd >>(read num rest
            cmd=(${(z)${(e):-\$jt$num}})
            terminal-title $cmd[1]:t) 2>/dev/null ;;
  (r)
    cmd=($(builtin history -n -1))
    terminal-title $cmd[1]:t ;;
  (sudo)
    terminal-title "$cmd[1]:t $cmd[2]:t" ;;
  (exec)
    shift cmd
    terminal-title $cmd[1]:t ;;
  (*)
    terminal-title $cmd[1]:t ;;
esac
