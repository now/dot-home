emulate -L zsh
setopt extendedglob

autoload -U zle/util/select-match-from-menu

local search=
local -aU matches

search=${LBUFFER//(#m)[\][()\\*?#<>~^]/\\$MATCH}
if [[ $WIDGET = *-space* ]]; then
  # We need to quote metacharacters in the search string since they are
  # otherwise active in the reverse subscript.  We need to avoid quoting other
  # characters since they aren’t and just stay quoted, rather annoyingly.
  search=${search// /*}
fi

matches=(${(Mo)*:#${NUMERIC+*}${search}*})
# Filter out any match that’s the same as the original.  Note that this isn’t a
# pattern this time.
matches=(${matches:#${LBUFFER}})

local REPLY=
zle/util/select-match-from-menu $matches || return $?
local line=$REPLY

integer newcursor
if [[ $WIDGET != *-end* ]]; then
  if (( ${+NUMERIC} )); then
    # Advance cursor so that it's still after the string typed
    local -a match mbegin mend
    if [[ $line = (#b)(*${LBUFFER})* ]]; then
       newcursor=${#match[1]}
    fi
  else
    newcursor=$CURSOR
  fi
fi
(( !newcursor )) && [[ $WIDGET = *-end* ]] && (( newcursor = ${#line} ))

typeset -ga reply; reply=($line $newcursor)
