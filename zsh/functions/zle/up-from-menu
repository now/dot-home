emulate -L zsh
setopt extendedglob

autoload -U split-shell-arguments
autoload -U zle/select-match-from-menu

local -a reply
local REPLY= REPLY2=
split-shell-arguments
integer word=$REPLY
integer char=$REPLY2
if (( CURSOR == $#BUFFER )); then
  (( char = ${#reply[word]} + 1 ))
fi

local -a words
integer i
for (( i = 1; i <= $#reply; i++ )); do
  words+={$reply[i]}
done

local left=
local arg=
if (( word % 2 == 1 )); then
  if [[ $BUFFER[CURSOR] != [[:space:]] ]]; then
    (( word-- ))
    arg=${reply[word]:-$PWD}
  else
    arg=$PWD
  fi
  left=${(j..)reply[1,word-1]}${reply[word][1,char-1]}
else
  if (( char == 1 )); then
    arg=$PWD
  else
    arg=${reply[word][1,$char-1]}
  fi
  left=${(j..)reply[1,word-1]}
fi

arg="$(print -r -- $arg(:a))"
local -a ups
for part in ${${(s./.)arg}[1,-2]}; do
  ups+=${ups[-1]}/$part
done
ups=(${(O)ups})
if [[ $arg != $PWD ]]; then
  ups+=$arg
fi

local REPLY=
zle/select-match-from-menu $ups || return $?

integer wordoff=${(c)#left}
LBUFFER="$left$REPLY/"
(( CURSOR = $#left + $#REPLY + 1 ))

zle list-choices
