emulate -L zsh
setopt extendedglob

integer n=${#*}
if (( n == 0 )); then
  return 1
elif (( n == 1 )); then
  declare -g REPLY; REPLY=$*[1]
  return
fi

integer width=$#n
local -a display
integer i
display=(${*/(#m)*/${(l.$width..0.):-$((++i))} $MATCH})

local chars
until (( $#chars == $width )); do
  zle -R "Select match: $chars" $display
  local char=
  read -k char
  case $char in
    ([$'\b'$'\x7f']) chars=$chars[1,-2] ;;
    ([[:digit:]]) chars+=$char ;;
    (*) chars=0; break ;;
  esac
done
# NOTE: The only way of clearing the display appears to be to overwrite it
# completely.
zle -R '' ${display//?/ }

[[ $chars -eq 0 || $chars -gt $n ]] && return 1
declare -g REPLY; REPLY=$*[$chars]
